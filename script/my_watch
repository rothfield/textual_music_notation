#!/usr/bin/env fish

set SCRIPT_DIR (dirname (status -f))
set PROJECT_ROOT (realpath "$SCRIPT_DIR/..")
cd $PROJECT_ROOT

set -g CMD go run .
set -g pgid -1

function cleanup --on-event fish_exit
    if test $pgid -gt 0
        echo "[watch.fish] Cleaning up process group $pgid"
        command kill -9 -$pgid 2>/dev/null
    end
end

function run_command
    if test $pgid -gt 0
        echo "[watch.fish] Killing previous process group $pgid"
        command kill -9 -$pgid 2>/dev/null
    end

    echo "[watch.fish] Starting: $CMD"
    fish -c "cd $PROJECT_ROOT; exec setsid $CMD > /tmp/watch.log 2>&1" &
    set -g pgid $last_pid
end

echo "[watch.fish] Watching $PROJECT_ROOT (excluding .git) for *.go, *.js, *.html, *.css..."
run_command

while true
    inotifywait -r -e modify,create,delete,move . \
        --exclude '/\.git/' --format '%w%f' |
    while read changed
        if echo $changed | grep -qE '\.go$|\.js$|\.html$|\.css$'
            clear
            echo "[watch.fish] Change detected: $changed"
            run_command
        end
    end
end

