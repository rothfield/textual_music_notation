#!/usr/bin/env fish

set SCRIPT_DIR (dirname (status -f))
set PROJECT_ROOT (realpath "$SCRIPT_DIR/..")
cd $PROJECT_ROOT

function kill_port_8080
    set pids (lsof -ti :8080)
    if test (count $pids) -gt 0
        echo "[watch.fish] Killing processes on port 8080: $pids"
        for pid in $pids
            echo " - killing PID $pid"
            kill -9 $pid
        end
    end
end

function cleanup --on-event fish_exit
    echo "[watch.fish] Cleanup on exit"
    kill_port_8080
end

function run_command
    kill_port_8080
    echo "[watch.fish] Starting: go run ."
    go run . &
end

echo "[watch.fish] Watching $PROJECT_ROOT (excluding .git) for *.go, *.js, *.html, *.css..."
run_command

while true
    inotifywait -r -e modify,create,delete,move . \
        --exclude '/\.git/' --format '%w%f' |
    while read changed
        if echo $changed | grep -qE '\.go$|\.js$|\.html$|\.css$'
            clear
            echo "[watch.fish] Change detected: $changed"
            run_command
        end
    end
end

